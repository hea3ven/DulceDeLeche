import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    dependencies {
        classpath  'com.squareup.okhttp3:okhttp:3.8.1'
        classpath  'com.j256.simplemagic:simplemagic:1.14'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'fabric-loom' version "0.1.0-SNAPSHOT"
    id 'org.ajoberstar.grgit' version '1.4.1'
    id 'com.matthewprenger.cursegradle' version '1.1.2'
    id 'com.github.breadmoirai.github-release' version '2.2.2-dev'
}

archivesBaseName = "dulcedeleche"
version = project_version + '-' + (project.hasProperty('BUILD_NO') ? project.BUILD_NO : 'SNAPSHOT')

repositories {
    mavenLocal()
    maven {
        name = "Hea3veN"
        url = "https://raw.github.com/hea3ven/mvn-repo/master"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:$version_mc_jar"
    mappings "net.fabricmc:yarn:$version_mc_mappings"
    modCompile "net.fabricmc:fabric-loader:$version_fabric_loader"

    modCompile "net.fabricmc:fabric:$version_fabric"

    modCompile "net.fabricmc:fabric-language-kotlin:$version_fabric_kotlin"
    compileOnly "net.fabricmc:fabric-language-kotlin:$version_fabric_kotlin"

    testCompile "junit:junit:4.12"
}

minecraft {
}

processResources {
    filesMatching ('**/*.json') {
        filter ReplaceTokens, tokens: [
            version: project.version.toString(),
            version_fabric: "$version_fabric".toString(),
            version_kotlin: "$version_kotlin".toString(),
            version_fabric_kotlin: "$version_fabric_kotlin".toString()
            ]
    }
}

curseforge {
    apiKey = project.hasProperty('curse_api_key') ? project.curse_api_key : ''

    options {
        forgeGradleIntegration = false
    }

    project {
        id = project_curseforge_id
        releaseType = 'release'
        changelog = project.hasProperty('changelog') ? project.changelog : ''
        addGameVersion version_mc
    }
}

githubRelease {
    token project.hasProperty('github_release_token') ? project.github_release_token : ''
    owner "Hea3veN"
    repo "DulceDeLeche"
    targetCommitish "master"
    body project.hasProperty('changelog') ? project.changelog : ''
    draft false
    prerelease false
    releaseAssets jar.outputs.allOutputFiles
}

